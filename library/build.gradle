apply plugin: 'com.android.library'

android {
    compileSdkVersion COMPILE_SDK_VERSION as int
    buildToolsVersion BUILD_TOOLS_VERSION as String

    defaultConfig {
        minSdkVersion 9
        targetSdkVersion COMPILE_SDK_VERSION as int
        versionCode VERSION_CODE as int
        versionName VERSION_NAME as String
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    lintOptions {
        // don't need parcel creator for the sub-class of MessageSnapshot.
        warning 'ParcelCreator'

        // avoid warning for "Not all execution paths return a value"
        return true
    }
}


plugins.apply('checkstyle')

task('checkstyle', type: Checkstyle) {
    configFile rootProject.file('checkstyle.xml')
    source 'src/main/java'
    source 'src/test/java'
    ignoreFailures false
    showViolations true
    include '**/*.java'

    classpath = files()
}

afterEvaluate {
    tasks.findByName('check').dependsOn('checkstyle')
}

checkstyle {
    toolVersion = '6.18'
}

dependencies {
    testImplementation "junit:junit:4.12"
    //noinspection GradleDynamicVersion
    testImplementation "org.mockito:mockito-core:2.+"
    testImplementation "org.robolectric:robolectric:3.3.2"
}

if (isForUpload2Maven()) {
    println ':library:include upload 2 maven methods'
    apply from: 'https://raw.githubusercontent.com/Jacksgong/JCenter/master/installv1.gradle'
    apply from: 'https://raw.githubusercontent.com/Jacksgong/JCenter/master/bintrayv1.gradle'
}
