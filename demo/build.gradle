apply plugin: 'com.android.application'

android {
    signingConfigs {
        release {
            keyAlias 'FileDownloaderDemoKey'
            keyPassword 'liulishuo'
            storeFile file('filedownloaderdemo.jks')
            storePassword 'liulishuo'
        }
    }
    compileSdkVersion COMPILE_SDK_VERSION as int
    buildToolsVersion BUILD_TOOLS_VERSION as String

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion COMPILE_SDK_VERSION as int

        File file = project.rootProject.file('local.properties');
        def needLog = null
        if (file.exists()) {
            Properties p = new Properties()
            p.load(file.newDataInputStream())
            needLog = p.getProperty("needLog")
        }
        buildConfigField "boolean", "DOWNLOAD_NEED_LOG", needLog == "true" ? "true" : "false"
    }
    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }

        debug {
            minifyEnabled false
        }
    }
    lintOptions {
        // This seems to be firing due to okio referencing java.nio.File
        // which is harmless for us.
        warning 'InvalidPackage'
    }
}

dependencies {
    implementation 'com.android.support:appcompat-v7:28.0.0'
    implementation 'com.android.support:recyclerview-v7:28.0.0'
    implementation 'com.android.support:design:28.0.0'
    debugImplementation 'cn.dreamtobe.threaddebugger:threaddebugger:1.3.3'
    releaseImplementation 'cn.dreamtobe.threaddebugger:threaddebugger-no-op:1.3.3'
    implementation project(':library')
    // for testing
    implementation 'com.squareup.okio:okio:1.14.0'
}
